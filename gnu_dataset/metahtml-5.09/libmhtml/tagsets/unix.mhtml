;;; unix.mhtml: -*- Meta-HTML -*-  DESCRIPTIVE TEXT.
;;;
;;; Copyright (c) 1998 Brian J. Fox
;;; Author: Brian J. Fox (bfox@ai.mit.edu) Fri Oct  2 13:02:48 1998.

<defun unix::hostname>
  <set-var hostprog = <path::find-executable hostname>>
  <when <get-var-once hostprog>>
    <set-var host[] = <cgi-exec /bin/hostname>>
    <get-var host>
  </when>
</defun>

<defun unix::get-passwd-entry username>
  <with-open-stream s /etc/passwd>
    <stream-get-contents s passwords>
  </with-open-stream>
  <coerce-var passwords type=array>
  <foreach line passwords>
    <unset-var info>
    <set-var info=<subst-in-string <get-var-once line> ":" "\n">>
    <coerce-var info type=array>
    <when <string-eq <get-var-once username> <get-var-once info[0]>>>
      <in-package local>
	<set-var
	  username = <get-var ^::info[0]>
	  password = <get-var ^::info[1]>
	  uid = <get-var ^::info[2]>
	  gid = <get-var ^::info[3]>
	  gecos = <get-var ^::info[4]>
	  homedir = <get-var ^::info[5]>
	  shell = <get-var ^::info[6]>>
	<package-to-alist>
      </in-package>
      <break>
    </when>
  </foreach>
</defun>

<defun unix::get-group-entry username>
  <set-var loaded=<dir::read-file /etc/group groupdata>>
  <coerce-var groupdata type=array>
  <foreach line groupdata>
    <unset-var info>
    <set-var info=<subst-in-string <get-var-once line> ":" "\n">>
    <coerce-var info type=array>
    <when <string-eq <get-var-once username> <get-var-once info[0]>>>
      <in-package local>
	<set-var
	  username = <get-var ^::info[0]>
	  password = <get-var ^::info[1]>
	  gid = <get-var ^::info[2]>
	  users = <get-var ^::info[3]>>
	<subst-in-var users "[ \t,]+" "\n">
	<coerce-var users type=array>
	<package-to-alist>
      </in-package>
      <break>
    </when>
  </foreach>
</defun>

