dnl --------------------------------------------------------------------
dnl configure.in --- GNU autoconf source for toplevel directory.
dnl
dnl Copyright (C) 1997-2006 Bruce Korb
dnl
dnl Time-stamp:      "2006-12-02 10:43:05 bkorb"
dnl Author:           Bruce Korb <bkorb@gnu.org>
dnl Maintainer:       Bruce Korb <bkorb@gnu.org>
dnl Created:          Mon Jun 30 15:10:13 1997
dnl            by: bkorb
dnl --------------------------------------------------------------------
dnl Code: $Id: configure.in,v 4.18 2006/12/10 16:43:00 bkorb Exp $
dnl --------------------------------------------------------------------
dnl
AC_INIT([GNU AutoGen],[5.9],[autogen-users@lists.sourceforge.net])
AC_CONFIG_SRCDIR(agen5/autogen.c)
AC_CONFIG_AUX_DIR(config)
AC_CANONICAL_TARGET
[. $srcdir/VERSION
d=`dirname $0`
ag_top_srcdir=`cd $d && pwd`
ag_top_builddir=`pwd`]
AM_INIT_AUTOMAKE([gnu check-news 1.5 dist-bzip2])
AC_LIBTOOL_WIN32_DLL    m4_define(AC_PROVIDE_AC_LIBTOOL_WIN32_DLL)
AC_PROG_LIBTOOL
ifdef([AC_REVISION],AC_REVISION($Revision: 4.18 $),)dnl
[config_start_time=`date +%s 2>/dev/null`]
# ----------------------------------------------------------------------
# Substitute VERSION vars here, so that they can be used by the Makefile
# ----------------------------------------------------------------------
AC_SUBST(AG_VERSION)
AC_SUBST(AG_MAJOR_VERSION)
AC_SUBST(AG_MINOR_VERSION)
AC_SUBST(AO_CURRENT)
AC_SUBST(AO_REVISION)
AC_SUBST(AO_AGE)
AC_SUBST(GO_CURRENT)
AC_SUBST(GO_REVISION)
AC_SUBST(GO_AGE)
AC_DEFINE_UNQUOTED(AO_CURRENT,$AO_CURRENT,
                   [Define this to the autoopts current interface number])
AC_DEFINE_UNQUOTED(AO_REVISION,$AO_REVISION,
                   [Define this to the autoopts interface revision number])
AC_DEFINE_UNQUOTED(AO_AGE,$AO_AGE,
                   [Define this to the autoopts interface age number])

# ----------------------------------------------------------------------
# Set up the environment to configure the snprintv subpackage using
# this version of AutoGen (as opposed to any installed version).
# ----------------------------------------------------------------------
ac_configure_args="$ac_configure_args --enable-snprintfv-convenience"
ag_srcdir=`\cd $srcdir && pwd`
if test x$ag_srcdir != x && test -d $ag_srcdir; then
  :
else
  ag_srcdir=..
fi

# ----------------------------------------------------------------------
# If `configure' is invoked (in)directly via `make', ensure that it
# encounters no `make' conflicts.  Ignore error if shell does not have
# unset, but at least set these to empty values.
# ----------------------------------------------------------------------
MFLAGS=
MAKEFLAGS=
MAKELEVEL=
unset MFLAGS MAKEFLAGS MAKELEVEL 2>/dev/null

AM_WITH_DMALLOC

# ----------------------------------------------------------------------
# check for various programs used during the build.
# ----------------------------------------------------------------------
# =================
# AC_PROG_CC
# =================
AC_PROG_CC
# =================
# AC_PROG_CC_STDC
# =================
AC_PROG_CC_STDC
# =================
# AC_EXEEXT
# =================
AC_EXEEXT
# =================
# AC_PROG_INSTALL
# =================
AC_PROG_INSTALL
# =================
# AC_PROG_LIBTOOL
# =================
AC_PROG_LIBTOOL
# =================
# AC_CHECK_PROG
# =================
AC_CHECK_PROG(TEXI2HTML, texi2html, texi2html, :)
# =================
# AC_C_CONST
# =================
AC_C_CONST
# =================
# AC_C_INLINE
# =================
AC_C_INLINE

# ----------------------------------------------------------------------
# Check for Dynamic library stuff
# ----------------------------------------------------------------------
AC_CHECK_LIB(dl, dlopen)

# ----------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
# ----------------------------------------------------------------------
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T
AC_C_LONG_DOUBLE
if test x$ac_cv_type_long_double = xno; then
  snv_long_double=double
else
  snv_long_double='long double'
fi

AC_DEFINE_UNQUOTED([SNV_LONG_DOUBLE],$snv_long_double,
                   [Define this to the long+double type])
AC_CHECK_TYPES([u_int, long long, uintmax_t])
AC_CHECK_SIZEOF(char*, 4)
AC_CHECK_SIZEOF(int,   4)
AC_CHECK_SIZEOF(long,  4)
AC_CHECK_SIZEOF(short, 2)

# ----------------------------------------------------------------------
# Checks for various functions.
# ----------------------------------------------------------------------
AC_CHECK_FUNCS(strchr strsignal strlcpy snprintf dlopen)
AC_SEARCH_LIBS(copysign, [m],
               [AC_DEFINE(HAVE_COPYSIGN, 1,
                          [Define to 1 if you have the `copysign' function.])])
AC_SEARCH_LIBS(copysignl, [m],
               [AC_DEFINE(HAVE_COPYSIGNL, 1,
                          [Define to 1 if you have the `copysignl' function.])])
AC_SEARCH_LIBS(modfl, [m],
               [AC_DEFINE(HAVE_MODFL, 1,
                          [Define to 1 if you have the `modfl' function.])])

# ----------------------------------------------------------------------
# Check for the functions needed from libgen and libdl
# ----------------------------------------------------------------------

AM_CONDITIONAL([NEED_PATHFIND], [test X$ac_cv_func_pathfind = Xyes])
[if test X$ac_cv_func_dlopen = Xyes
then DYNAMIC_AG=-export-dynamic
else DYNAMIC_AG=""
fi]
AC_SUBST(DYNAMIC_AG)

# ----------------------------------------------------------------------
# Do all our own macros
# ----------------------------------------------------------------------
INVOKE_AG_MACROS
[
if ! test x$ag_cv_sys_siglist = xyes
then
  if ! test x$ac_cv_func_strsignal = xyes
  then
    echo "WARNING:  strsignal will use POSIX names and Linux signal numbers"
  fi
fi >&2
if test "X${ag_cv_link_sigsetjmp}" = Xno
then]
  AC_MSG_ERROR([AutoGen requires sigsetjmp(3)])[
fi
]
# ----------------------------------------------------------------------
# Do SNPRINTFV macros
# ----------------------------------------------------------------------
INVOKE_SNPRINTFV_MACROS
# ----------------------------------------------------------------------
# Generate the make files.
# ----------------------------------------------------------------------
AC_PROG_LN_S
[
test -f ./snprintfv/snprintfv/snprintfv.h \
  && rm -f ./snprintfv/snprintfv.h \
  && ${LN_S} `pwd`/snprintfv/snprintfv/snprintfv.h ./snprintfv/

AGnam=autogen${ac_exeext}
GDnam=getdefs${ac_exeext}
CLnam=columns${ac_exeext}
AGexe=${ag_top_builddir}/agen5/${AGnam}
GDexe=${ag_top_builddir}/getdefs/${GDnam}
CLexe=${ag_top_builddir}/columns/${CLnam}
M4_SRC=`cd $srcdir/config ; echo [a-z]*.m4`

config_end_time=`date +%s 2>/dev/null`
time_delta=`expr ${config_end_time} - ${config_start_time} 2>/dev/null`

if test -z "${time_delta}"
then time_delta=10
elif test ${time_delta} -lt 5
then time_delta=5 ; fi

AG_TIMEOUT=${time_delta}
]
AC_DEFINE_UNQUOTED(AG_DEFAULT_TIMEOUT, ${time_delta},
                   [define to suitable timeout limit for shell command])
AC_SUBST(M4_SRC)
AC_SUBST(AGnam)
AC_SUBST(GDnam)
AC_SUBST(CLnam)
AC_SUBST(AGexe)
AC_SUBST(GDexe)
AC_SUBST(CLexe)
AC_SUBST(AG_TIMEOUT)
AC_SUBST(ac_aux_dir)
AC_SUBST(LIBS)
[
if test "$ag_top_srcdir" = "$ag_top_builddir"
then
 INCLIST='-I${top_builddir} -I${top_srcdir}/autoopts'
else
 INCLIST='-I${top_builddir} -I${top_srcdir}'
 INCLIST="${INCLIST} -I\${top_builddir}/autoopts -I\${top_srcdir}/autoopts"
fi
]
AC_SUBST(INCLIST)
[conf_shell=${CONFIG_SHELL-/bin/sh}]
AC_SUBST(conf_shell)


AM_CONFIG_HEADER(config.h:config-h.in)
AC_CONFIG_FILES([doc/mk-agen-texi.sh:doc/mk-agen-texi-in],
                [chmod +x doc/mk-agen-texi.sh])
AC_CONFIG_FILES([\
    Makefile
    agen5/Makefile
    agen5/test/Makefile
    autoopts/Makefile
    autoopts/autoopts-config
    autoopts/mk-autoopts-pc
    autoopts/test/Makefile
    autoopts/test/defs
    columns/Makefile
    compat/Makefile
    doc/Makefile
    getdefs/Makefile
    getdefs/test/Makefile
    pkg/Makefile
    pkg/pkg-env
    snprintfv/Makefile
    xml2ag/Makefile
    xml2ag/test/Makefile ])

AC_CONFIG_COMMANDS([stamp-h],
[test -z "$CONFIG_HEADERS" || echo timestamp > stamp-h])
AC_OUTPUT

echo \
"------------------------------------------------------------------------
Configuration:

  Source code location:   ${srcdir}
  Compiler:               ${CC}
  Compiler flags:         ${CFLAGS}
  Host System Type:       ${host}
  Install path:           ${prefix}

  See config.h for further configuration information.
------------------------------------------------------------------------"
dnl Local Variables:
dnl mode: autoconf-mode
dnl indent-tabs-mode: nil
dnl sh-indentation: 2
dnl End:
dnl
dnl end of configure.am

dnl configure.in ends here
