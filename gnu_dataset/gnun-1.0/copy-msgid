#! /bin/sed -f

# Copyright (C) 2012 Free Software Foundation, Inc.

# This file is part of GNUnited Nations.

# GNUnited Nations is free software: you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation, either version 3 of the
# License, or (at your option) any later version.

# GNUnited Nations is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with GNUnited Nations.  If not, see <http://www.gnu.org/licenses/>.

# Copy current msgid as `previous' when the latter is absent.

/^#.*, fuzzy/ {
  p; s/.*//;
  :collect; N;
  # There is already previous msgid; skip the string.
  /\n#| [^\n]*$/ b skip;
  # Leave comments as is.
  /\n#/ { s/\n//; p; s/.*//; b collect; }
  # Collect lines until msgstr is found.
  /\nmsgstr "[^\n]*$/! b collect;
  s/^\n//; h;
  s/\nmsgstr "[^\n]*$//; s/\(\n\)\|\(^\)/\n#| /g;
  G
  :skip; s/^\n//;
}
